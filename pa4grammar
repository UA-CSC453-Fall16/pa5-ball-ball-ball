Goal    ::= "import" "meggy.Meggy" ";" MainClass ( ClassDeclaration )* <EOF>
MainClass   ::= "class" Identifier "{" "public" "static" "void" "main" "(" "String" "[" "]" Identifier ")" "{" (Statement)* "}" "}"
ClassDeclaration    ::= "class" Identifier "{" ( MethodDeclaration )* "}"
MethodDeclaration   ::= "public" Type Identifier "(" ( Type Identifier ( "," Type Identifier )* )? ")" "{" ( Statement )* ("return" Expression ";")? "}"
Type    ::= "boolean"
|   "byte"
|   "int"
|   "void"
|   "Meggy.Color"
|   "Meggy.Button"
|   "Meggy.Tone"
Statement   ::= "{" ( Statement )* "}"
|   "Meggy.setPixel" "(" Expression "," Expression "," Expression ")" ";"
|   "Meggy.toneStart" "(" Expression "," Expression ")" ";"
|   "Meggy.delay" "(" Expression ")" ";"
|   Expression "." Identifier "(" ( Expression ( "," Expression )* )? ")" ";"
|   "if" "(" Expression ")" Statement
|   "if" "(" Expression ")" Statement "else" Statement
|   "while" "(" Expression ")" Statement
Expression  ::= Expression ( "&&" | "<" | "==" | "+" | "-" | "*" ) Expression
|   "-" Expression
|   Expression "." Identifier "(" ( Expression ( "," Expression )* )? ")"
|   "Meggy.getPixel" "(" Expression "," Expression ")"
|   "Meggy.checkButton" "(" Expression ")"
|   "(" "byte" ")" Expression
|   <INT_LITERAL>
|   <COLOR_LITERAL>
|   <BUTTON_LITERAL>
|   <TONE_LITERAL>
|   "true"
|   "false"
|   Identifier
|   "this"
|   "new" Identifier "(" ")"
|   "!" Expression
|   "(" Expression ")"
Identifier  ::= <IDENTIFIER>
